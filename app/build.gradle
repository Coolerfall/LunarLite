apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.squareup.sqldelight'

def versionMajor = 0
def versionMinor = 1
def versionPatch = 0

android {
	compileSdkVersion versions.compileSdk
	buildToolsVersion versions.buildTools

	defaultConfig {
		minSdkVersion versions.minSdk
		targetSdkVersion versions.targetSdk
		versionCode versionMajor * 1000 + versionMinor * 100 + versionPatch
		versionName "${versionMajor}.${versionMinor}.${versionPatch}"
	}

	signingConfigs {
		debug {
			storeFile file("../debug.keystore")
			storePassword "lunardebug"
			keyAlias "android"
			keyPassword "lunardebug"
		}

		release {}
	}

	buildTypes {
		debug {
			minifyEnabled false
			signingConfig signingConfigs.debug
			applicationIdSuffix '.debug'
			proguardFiles 'proguard-rules.pro'
		}

		release {
			signingConfig signingConfigs.release
			minifyEnabled true
			proguardFiles 'proguard-rules.pro', getDefaultProguardFile('proguard-android.txt')
		}
	}

	productFlavors {
		internal {
			applicationId "com.coolerfall.lunarlite.internal"
		}

		production {
			applicationId "com.coolerfall.lunarlite"
		}
	}

	lintOptions {
		abortOnError false
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
}

dependencies {
	compile libs.appcompatv7
	provided libs.supportAnnotation

	compile libs.dagger
	apt libs.daggerCompiler
	apt libs.butterKnifeCompiler
	apt libs.autoValue
	provided libs.autoValueAnnotations

	compile libs.rxJava
	compile libs.rxAndroid
	compile libs.butterKnife
	compile libs.sqlbrite
	compile libs.timber
	compile libs.phrase
	compile libs.greenDao
	compile libs.lunarView
}
