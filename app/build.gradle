apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.squareup.sqldelight'

def versionMajor = 0
def versionMinor = 2
def versionPatch = 0
def versionBuild = 1

android {
  compileSdkVersion rootProject.ext.compileSdkVersion
  buildToolsVersion rootProject.ext.buildToolsVersion

  defaultConfig {
    applicationId "com.coolerfall.lunarlite"
    minSdkVersion rootProject.ext.minSdkVersion
    targetSdkVersion rootProject.ext.targetSdkVersion

    versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
    versionName "${versionMajor}.${versionMinor}.${versionPatch}"

    resConfigs "en", "zh-rCN"
  }

  lintOptions {
    textReport true
    textOutput 'stdout'
    warning 'MissingTranslation'
    fatal 'UnusedResources'
    abortOnError false
  }

  packagingOptions {
    exclude 'LICENSE.txt'
  }

  signingConfigs {
    debug {
      storeFile file("../debug.keystore")
      storePassword "lunardebug"
      keyAlias "android"
      keyPassword "lunardebug"
    }
  }

  buildTypes {
    release {
      minifyEnabled true
      proguardFiles 'proguard-rules.pro'
    }

    debug {
      minifyEnabled false
      applicationIdSuffix '.debug'
      proguardFiles 'proguard-rules.pro'
    }
  }

  flavorDimensions "free"
  productFlavors {
    internal {
      applicationIdSuffix '.internal'
    }

    production {
    }
  }
}

dependencies {
  implementation rootProject.ext.kotlinLib

  compileOnly rootProject.ext.supportAnnotations
  implementation rootProject.ext.appcompatV7

  compileOnly rootProject.ext.javaxInject
  compileOnly rootProject.ext.javaxAnnotations
  implementation rootProject.ext.dagger
  implementation rootProject.ext.daggerAndroid
  implementation rootProject.ext.daggerAndroidSupport
  kapt rootProject.ext.daggerCompiler

  implementation rootProject.ext.rxjava2
  implementation rootProject.ext.rxAndroid2
  implementation rootProject.ext.sqlbrite2

  implementation rootProject.ext.butterKnife
  kapt rootProject.ext.butterKnifeCompiler

  implementation rootProject.ext.timber
  implementation rootProject.ext.phrase
  implementation rootProject.ext.lunarView
}
